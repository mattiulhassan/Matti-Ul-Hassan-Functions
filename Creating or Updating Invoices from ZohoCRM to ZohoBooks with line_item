organizationID = "684890986";
crm_invoice = zoho.crm.getRecordById("Invoices",id);
contact_criteria = crm_invoice.get("Contact_Name").get("id");
books_invoice_map = Map();
// books_invoice_map.put("invoice_number",crm_invoice.get("Subject"));
searchParam = {"zcrm_contact_id":contact_criteria};
search_customer = zoho.books.getRecords("contacts",organizationID,searchParam,"books");
if(search_customer.size() > 0)
{
	customer_id = search_customer.get("contacts").getJSON("contact_id");
}
info "Customer ID in Books: " + customer_id;
books_invoice_map.put("customer_id",customer_id);
books_line_items = List();
crm_line_items = crm_invoice.get("Product_Details");
for each  line_item in crm_line_items
{
	crm_product = line_item.get("product");
	if(crm_product != null)
	{
		crm_product_id = crm_product.get("id");
		product_info = zoho.crm.getRecordById("Products",crm_product_id);
		rate = line_item.get("list_price");
		quantity = line_item.get("quantity");
		tax_amount = line_item.get("Tax");
		product_code = product_info.get("Product_Code");
		searchParam = {"item_name":product_info.get("Product_Name")};
		search_item = zoho.books.getRecords("items",organizationID,searchParam,"books");
		if(search_item.size() > 0 || search_item != Null)
		{
			books_item = search_item.get("items");
			books_item_id = books_item.getJSON("item_id");
		}
		item_map = Map();
		item_map.put("tax_amount",tax_amount);
		item_map.put("item_id",books_item_id);
		item_map.put("rate",rate);
		item_map.put("description",line_item.get("product_description"));
		item_map.put("quantity",line_item.get("quantity"));
		item_map.put("discount_total",line_item.get("total_after_discount"));
		books_line_items.add(item_map);
	}
}
invoice_update = Map();
books_invoice_map.put("date",crm_invoice.get("Invoice_Date"));
books_invoice_map.put("due_date",crm_invoice.get("Due_Date"));
books_invoice_map.put("status",crm_invoice.get("Status"));
books_invoice_map.put("line_items",books_line_items);
// searchParam = {"invoice_id":crm_invoice.get("Books_invoice_id")};
// search_invoice = zoho.books.getRecords("invoices",organizationID,searchParam,"books");
if(crm_invoice.get("Books_invoice_id") != Null)
{
	info "Updating";
	existing_invoice_id = crm_invoice.get("Books_invoice_id").toNumber();
	update_resp = zoho.books.updateRecord("invoices",organizationID,existing_invoice_id,books_invoice_map,"books");
	// 	info update_resp;
}
else
{
	info "Creating";
	create_resp = zoho.books.createRecord("invoices",organizationID,books_invoice_map,"books");
	info create_resp;
	invoice_id = create_resp.get("invoice").get("invoice_id");
	invoice_update.put("Books_invoice_id",invoice_id);
	crt = zoho.crm.updateRecord("Invoices",id,invoice_update);
}
